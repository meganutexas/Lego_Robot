package Libraries.Game.Graphics

// Using the Quorum error library to throw an error if the user attempts to use
// an uninitialized TextureFilter. Could potentially be used to also prevent
// changing the value after initialization.

use Libraries.Language.Errors.Error

/* 
This class is used for internal use by the Game class to define the filtering on
a Texture in OpenGL. Most users will never need to touch this class directly.
*/
class TextureFilter 
    private integer value = 0
    private boolean initialized = false

    /* Constant values here correspond to the following constant values
       from GraphicsManager:

       NEAREST = GL_NEAREST
       LINEAR = GL_LINEAR
       MIMAP = GL_LINEAR_MIPMAP_LINEAR
       MIPMAP_NEAREST_NEAREST = GL_NEAREST_MIPMAP_NEAREST
       MIPMAP_LINEAR_NEAREST = GL_LINEAR_MIPMAP_NEAREST
       MIPMAP_NEAREST_LINEAR = GL_NEAREST_MIPMAP_LINEAR
       MIPMAP_LINEAR_LINEAR = GL_LINEAR_MIPMAP_LINEAR

       While currently this code has an extra copy of the constants, an
       alternative approach would be to declare a GraphicsManager variable here and
       access the constants through that.
    */

    public constant integer NEAREST = 9728
    public constant integer LINEAR = 9729
    public constant integer MIPMAP = 9987
    public constant integer MIPMAP_NEAREST_NEAREST = 9984
    public constant integer MIPMAP_LINEAR_NEAREST = 9985
    public constant integer MIPMAP_NEAREST_LINEAR = 9986
    public constant integer MIPMAP_LINEAR_LINEAR = 9987

    /*
    Returns the current value of the TextureFilter. Calling this before the
    TextureFilter has been initialized with ConstructTextureFilter will result
    in an error.
    */
    public action GetValue returns integer
        if initialized = false
            Error error
            error:SetErrorMessage("The value must be initialized before using GetValue! Initialize the value with ConstructTextureFilter.")
            alert(error)
        end

        return value
    end

    /*
    This action sets the value of the TextureFilter. Note that the TextureFilter
    can not be used before using this action.
    */
    public action ConstructTextureFilter(integer type)

        // We could potentially use error checking here to prevent:
        // 1. Initializing the value to something that doesn't make sense
        // 2. Calling this function after already initializing the TextureFilter
        initialized = true
        value = type
    end

    /*
    This action returns whether the currently set value indicates that the
    Texture is used as part of mip-mapping.
    */
    public action IsMipMap returns boolean
            return value not= NEAREST and value not= LINEAR
    end
end