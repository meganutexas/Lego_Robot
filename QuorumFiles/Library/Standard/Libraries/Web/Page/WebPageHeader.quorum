package Libraries.Web.Page

use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
use Libraries.Web.Page.WebGenerator
use Libraries.Web.Page.GlobalAttributeAccepter
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.HeaderLink
use Libraries.Web.Page.Title
use Libraries.Web.Page.Script
use Libraries.Web.Page.Base

/*
The WebPageHeader class represents HTML's (Hypertext Markup Language) head tag 
which is a container class for any WebTags that belong in the
web pages header. 
You can find more information about this tag at:
<a href="http://www.w3schools.com/tags/tag_head.asp">The head attribute</a>.

Attribute: Example
use Libraries.Web.Page.all
class Main
   action main
      //make a web page
      WebPage page
      //add a TextArea
      WebPageHeader header
      page:SetWebPageHeader(header)
      output page:Generate()
   end
end
*/
class WebPageHeader is WebGenerator, GlobalAttributeAccepter

    Title title
    List<HeaderLink> linkList
    List<MetaData> metaList
    List<Script> scriptList
    List<Base> baseList

    /*
        This action adds MetaData to the header.

        Attribute: Parameter value The header meta data. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.MetaData
        WebPageHeader header
        MetaData md
        header:Add(md)
        
    */
    action Add(MetaData value)
        metaList:Add(value)
    end

    /*
        This action removes the first instance of the meta data value found in the
        meta list.

        Attribute: Parameter value The meta data element. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.MetaData
        WebPageHeader header
        MetaData item
        header:Remove(item)
        
    */
    action Remove(MetaData value)
        metaList:Remove(value)
    end

    /*
        This action adds a link to a stylesheet to the header.

        Attribute: Parameter value The header meta data. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.HeaderLink
        WebPageHeader header
        HeaderLink md
        header:Add(md)
        
    */
    action Add(HeaderLink value)
        linkList:Add(value)
    end

    /*
        This action removes the first instance of the links to stylesheets found in the
        meta list.

        Attribute: Parameter value The meta data element. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.HeaderLink
        WebPageHeader header
        HeaderLink item
        header:Remove(item)
    */
    action Remove(HeaderLink value)
        linkList:Remove(value)
    end

    /*
        This action adds a link to a stylesheet in the header.

        Attribute: Parameter path The location of the stylesheet being added.
        
        Attribute: Returns The generated link for any further manipulation

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader

        WebPageHeader header
        header:AddStylesheet("style.css")
    */
    action AddStylesheet(text path) returns HeaderLink
        HeaderLink link
        link:SetRelationship("stylesheet")
        link:SetType("text/css")
        link:SetAddress(path)
        linkList:Add(link)
        return link
    end

    /*
        This action adds a link to an icon in the header. This is sometimes referred to as a "favicon".

        Attribute: Parameter path The location of the stylesheet being added. The file extension must be .ico
        
        Attribute: Returns The generated link for any further manipulation

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader

        WebPageHeader header
        header:AddIcon("favicon.ico")
    */
    action AddIcon(text path) returns HeaderLink
        HeaderLink link
        link:SetRelationship("icon")
        link:SetType("image/x-icon")
        link:SetAddress(path)
        linkList:Add(link)
        return link
    end

    /*
        This action adds Base to the header. There can only be one base
        on a page.

        Attribute: Parameter value The header base data. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Base
        WebPageHeader header
        Base base
        header:Add(base)
        
    */
    action Add(Base value)
        baseList:Add(value)
    end

    /*
        This action removes the base data from the header.

        Attribute: Parameter value The meta data element. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Base
        WebPageHeader header
        Base item
        header:Remove(item)
        
    */
    action Remove(Base value)
        baseList:Remove(value)
    end

    /*
        This action gets the number of meta data items in the header.

        Attribute: Returns The number of meta data items in the header. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        WebPageHeader header
        integer val = header:GetNumberOfMetaDataItems()
        
    */
    action GetNumberOfMetaDataItems returns integer
        if(metaList = undefined)
            return 0
        end
        return metaList:GetSize()
    end

    /*
        This action adds a script to the header of a webpage.

        Attribute: Parameter value The script to add to the header. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Script
        WebPageHeader header
        Script s
        header:Add(s)
        
    */
    action Add(Script value)
        scriptList:Add(value)
    end

    /*
        This action removes the first instance of the script from the headers 
        list of scripts.

        Attribute: Parameter value The script to be removed from the header. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Script
        WebPageHeader header
        Script s
        header:Add(s)
        header:Remove(s)
        
    */
    action Remove(Script value)
        scriptList:Remove(value)
    end

    /*
        This action gets the number of script items in the header.

        Attribute: Returns The number of script items in the header. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Script
        WebPageHeader header
        Script s
        header:Add(s)
        integer val = header:GetNumberOfScriptItems()
        
    */
    action GetNumberOfScriptItems returns integer
        if scriptList = undefined
            return 0
        end
        return scriptList:GetSize()
    end

    /*
        This action gets the headers title.

        Attribute: Returns The headers title. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Title
        WebPageHeader header
        Title title
        header:SetTitleHeader(title)
        Title t = header:GetTitleHeader()
        
    */
    action GetTitleHeader returns Title
        return title
    end

    /*
        This action sets the title for the header. This field is required.

        Attribute: Parameter title The title. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader
        use Libraries.Web.Page.Title
        WebPageHeader header
        Title title
        header:SetTitleHeader(title)
        
    */
    action SetTitleHeader(Title title)
        me:title = title
    end

    /*
        This action sets the text of the title in the header.

        Attribute: Parameter value The title text. 

        Attribute: Example
        use Libraries.Web.Page.WebPageHeader

        WebPageHeader header
        header:SetTitleHeaderValue("My Title")
        
    */
    action SetTitleHeaderValue(text value)
        title:AddText(value)
    end

    action Generate returns text
        text result = "<head " + me:GenerateAttributes() + ">" 

        if title not= undefined
            result = result + title:Generate()
        end
        
        Iterator<MetaData> it = metaList:GetIterator()
        repeat while it:HasNext()
            MetaData meta = it:Next()
            result = result + meta:Generate()
        end

        Iterator<Script> itScript = scriptList:GetIterator()
        repeat while itScript:HasNext()
            Script script = itScript:Next()
            result = result + script:Generate()
        end

        if baseList:GetSize() > 1
            alert("Can not have more than 1 base on a page")
        end

        Iterator<Base> itBase = baseList:GetIterator()
        repeat while itBase:HasNext()
            Base base = itBase:Next()
            result = result + base:Generate()
        end

        Iterator<HeaderLink> itLink = linkList:GetIterator()
        repeat while itLink:HasNext()
            HeaderLink style = itLink:Next()
            result = result + style:Generate()
        end
        
        return result + "</head>"
    end
end