package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Web.Page.Attribute
use Libraries.Web.Page.Attributes

/*
The OrderedList class represents HTML's (Hypertext Markup Language) ol tag which is
used to create an ordered list that can be ordered numerically or alphabetically.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_ol.asp">The ol attribute</a>.

Attribute: Example
use Libraries.Web.Page.all

class Main
    action main
      WebPage page

      OrderedList ol

      ListItem li
      li:SetDescription("A")
      
      ol:Add(li)

      page:AddToBody(ol)
      output page:Generate()
    end
end
*/
class OrderedList is WebTag
    constant text reversed = "reversed"
    constant text startValue = "start"
    constant text type = "type"

    public constant text numericalType = "1"
    public constant text alphabeticalLowerCaseType = "a"
    public constant text alphabeticalUpperCaseType = "A"
    public constant text romanNumberLowerCaseType = "i"
    public constant text romanNumberUpperCaseType = "I"

    /*
        This action sets the start value of the ordered list.
        

        Attribute: Parameter value The start value of the list.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetStartValue("3")
        
    */
    action SetStartValue(text value)
        AddAttribute(startValue, value)
    end

    /*
        This action gets the start value of the ordered list.
        

        Attribute: Returns The start value of the list.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetStartValue("3")
        Attribute startVal = ol:GetStartValue()
        
    */
    action GetStartValue returns Attribute
        return me:GetAttribute(startValue)
    end

    /*
        This action sets the type of marker on the ordered list(e.g. 1, A, a, I, i)
        

        Attribute: Parameter value The marker of the list.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetType("A")
        
    */
    action SetType(text value)
        AddAttribute(type, value)
    end

    /*
        This action gets the type of marker on the ordered list(e.g. 1, A, a, I, i)
        

        Attribute: Returns The marker of the list.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetType("A")
        Attribute type = ol:GetType()
        
    */
    action GetType returns Attribute
        return me:GetAttribute(type)
    end

    /*
        This action sets the ordered list to descending order when true.
        

        Attribute: Parameter value True for descending order false for assending order.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetReversed(true)
        
    */
    action SetReversed(boolean value)
        if value
            AddAttribute(reversed, reversed)
        else
            RemoveAttribute(reversed)
        end
    end

    /*
        This action gets wether the ordered list is in descending order or not.
        

        Attribute: Returns True for descending order false for assending order.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.OrderedList
        OrderedList ol
        ol:SetReversed(true)
        boolean isDescending = ol:GetReversed()
        
    */
    action GetReversed returns boolean
        return me:HasAttribute(reversed)
    end

    action Generate returns text
        text result = "<ol " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</ol>"
        return result
    end


    /*
        This action adds a ListItem with the specified text and to an OrderedList object.

        Attribute: Parameter listItemText specifies the text that will appear in the ListItem object

        Attribute: Returns the generated ListItem object for any further manipulation

        Attribute: Example

        use Libraries.Web.Page.all
        class Main
            action Main
                WebPage page
                OrderedList ol

                
                ol:AddListItem("List Item 1")
                page:Add(ol)

                text value = page:Generate()
                output value
            end
        end
    */
    action AddListItem(text listItemText) returns ListItem
        ListItem listItem
        listItem:AddText(listItemText)
        parent:WebTag:tags:Add(listItem)
        return listItem
    end

    /*
        This action adds a ListItem that has a Link inside of it with the specified link text and location to an OrderedList object.

        Attribute: Parameter linkText specifies the text that will appear in the ListItem object as a link

        Attribute: Parameter linkDestination specifies where the link leads to

        Attribute: Returns the generated ListItem object (which has the Link object inside of it) for any further manipulation

        Attribute: Example

        use Libraries.Web.Page.all
        class Main
            action Main
                WebPage page
                OrderedList ol

                
                ol:AddListItemLink("Quorum Reference Page", "www.quorumlanguage.com/reference.php")
                page:Add(ol)

                text value = page:Generate()
                output value
            end
        end
    */
    action AddListItemLink(text linkText, text linkDestination) returns ListItem
        Link link
        link:SetAddress(linkDestination)
        link:AddText(linkText)
        ListItem listItem
        listItem:Add(link)
        parent:WebTag:tags:Add(listItem)
        return listItem
    end


    /*
    This action places a Script object inside of an OrderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            Script script
            page:Add(script)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Script content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a HiddenContent object inside of an OrderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            HiddenContent hiddenContent
            page:Add(hiddenContent)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(HiddenContent content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a ListItem object inside of an OrderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page

            OrderedList ol

            ListItem li
            li:SetDescription("A")

            ol:Add(li)

            page:AddToBody(ol)
            output page:Generate()
        end
    end
    */
    action Add(ListItem content)
        parent:WebTag:tags:Add(content)
    end
end