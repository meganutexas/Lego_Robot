package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Web.Page.Attribute
use Libraries.Web.Page.Attributes
use Libraries.Web.Page.Option
use Libraries.Containers.Array
/*
The OptionGroup class represents HTML's (Hypertext Markup Language) optiongroup 
tag which is used to group options in a select or data list. 
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_optgroup.asp">The optgroup attribute</a>.

Attribute: Example
use Libraries.Web.Page.all
class Main
   action main
      WebPage page
      
      Select select
      OptionGroup og
      og:SetLabel("States")
      select:AddOptionGroup(og)

      page:AddToBody(select)
      output page:Generate()
   end
end
*/
class OptionGroup is WebTag
    constant text disabled = "disabled"
    constant text label = "label"
    Array<Option> options

    /*
        This action gets whether the option group in a datalist or select is disabled (true)
        or enabled(false).

        Attribute: Returns True if diabled and false if enabled. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Attribute

        OptionGroup og
        boolean result = og:GetDisabled()
        
    */
    action GetDisabled returns boolean
        Attribute d = GetAttribute(disabled)
        return  disabled = d:GetValue()
    end
    
    /*
        This action sets the option group to be disabled if true and enabled if false.

        Attribute: Parameter value True to disable and false to enable the option. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Attribute

        OptionGroup og
        og:SetDisabled(true)
        
    */
    action SetDisabled (boolean value)
        if value
            AddAttribute(disabled, disabled)
        else
            RemoveAttribute(disabled)
        end
    end

    /*
        This action gets the option groups labeling text.

        Attribute: Returns Labeling text. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Attribute

        OptionGroup og
        Attribute result = og:GetLabel()
        
    */
    action GetLabel returns Attribute
        return me:GetAttribute(label)
    end
    
    /*
        This action sets the option group label.

        Attribute: Parameter value The labeling text. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Attribute

        OptionGroup og
        og:SetLabel("states")
        
    */
    action SetLabel(text value)
        AddAttribute(label, value)
    end

    /*
        This action adds an option to the option group.

        Attribute: Parameter value The option to be added to the group. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Option

        OptionGroup og
        og:SetLabel("States")
        Option option
        option:SetLabel("Alabama")
        og:AddOption(option)
        
    */
    action AddOption(Option option)
        options:Add(option)
    end

    /*
        This action removes an option from the option group.

        Attribute: Parameter value The option to be removed from the group. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Option

        OptionGroup og
        og:SetLabel("States")
        Option option
        option:SetLabel("Alabama")
        og:AddOption(option)
        
        og:RemoveOption(option)
        
    */
    action RemoveOption(Option option)
        options:Remove(option)
    end

    /*
        This action gets the number of options in an option group.

        Attribute: Returns The number of options in an option group. 

        Attribute: Example
        use Libraries.Web.Page.OptionGroup
        use Libraries.Web.Page.Attribute

        OptionGroup og
        integer result = og:GetNumberOfOptions()
        
    */
    action GetNumberOfOptions returns integer
        return options:GetSize()
    end

    action Generate returns text
        text result = "<optgroup " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</optgroup>"
        return result
    end

    /*
    This action places a Option object inside of an OptionGroup object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            Option option
            page:Add(option)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Option content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a HiddenContent object inside of an OptionGroup object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            HiddenContent hiddenContent
            page:Add(hiddenContent)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(HiddenContent content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a Script object inside of an OptionGroup object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            Script script
            page:Add(script)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Script content)
        parent:WebTag:tags:Add(content)
    end

end