package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Web.Page.Attributes

/*
The UnorderedList class represents HTML's (Hypertext Markup Language) ul tag which is
used to add an unordered list to the page. Use ListItem to add to the list.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_ul.asp">The ul attribute</a>.

Attribute: Example

use Libraries.Web.Page.all
class Main
   action main
      WebPage page
      
      UnorderedList ul
      ListItem li
      li:SetDescription("Apple")
      ul:Add(li)

      page:AddToBody(ul)
      output page:Generate()
   end
end
*/
class UnorderedList is WebTag
    action Generate returns text
        text result = "<ul " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</ul>"
        return result
    end

    /*
        This action adds a ListItem with the specified text and to an UnorderedList object.

        Attribute: Parameter listItemText specifies the text that will appear in the ListItem object

        Attribute: Returns the generated ListItem object for any further manipulation

        Attribute: Example

        use Libraries.Web.Page.all
        class Main
            action Main
                WebPage page
                UnorderedList ul

                
                ul:AddListItem("List Item")
                page:Add(ul)

                text value = page:Generate()
                output value
            end
        end
    */
    action AddListItem(text listItemText) returns ListItem
        ListItem listItem
        listItem:AddText(listItemText)
        parent:WebTag:tags:Add(listItem)
        return listItem
    end

    /*
        This action adds a ListItem that has a Link inside of it with the specified link text and location to an UnorderedList object.

        Attribute: Parameter linkText specifies the text that will appear in the ListItem object as a link

        Attribute: Parameter linkDestination specifies where the link leads to

        Attribute: Returns the generated ListItem object (which has the Link object inside of it) for any further manipulation

        Attribute: Example

        use Libraries.Web.Page.all
        class Main
            action Main
                WebPage page
                UnorderedList ul

                
                ul:AddListItemLink("Quorum Reference Page", "www.quorumlanguage.com/reference.php")
                page:Add(ul)

                text value = page:Generate()
                output value
            end
        end
    */
    action AddListItemLink(text linkText, text linkDestination) returns ListItem
        Link link
        link:SetAddress(linkDestination)
        link:AddText(linkText)
        ListItem listItem
        listItem:Add(link)
        parent:WebTag:tags:Add(listItem)
        return listItem
    end

    /*
    This action places a Script object inside of an UnorderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            Script script
            page:Add(script)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Script content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a HiddenContent object inside of an UnorderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            

            HiddenContent hiddenContent
            page:Add(hiddenContent)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(HiddenContent content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a ListItem object inside of an UnorderedList object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page

            OrderedList ol

            ListItem li
            li:SetDescription("A")

            ol:Add(li)

            page:AddToBody(ol)
            output page:Generate()
        end
    end
    */
    action Add(ListItem content)
        parent:WebTag:tags:Add(content)
    end
end